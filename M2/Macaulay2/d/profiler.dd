-- Copyright 2024 by Mahrud Sayrafi

use binding;  -- for symbols
use evaluate; -- for eval
use chrono;   -- for wallTimer
use sets;     -- for Tally

header "
#include <thread>
#include <vector>
#include <iostream>

// defined in main.cpp, uses Boost::stacktrace
void profiler_stacktrace(std::ostream &, int);

// the top-level runtime stack
thread_local std::vector<stdiop0_Position> M2_stack;";

-- TODO: implement better std::vector utilities
-- push and pop the given positions in the top-level call stack log
stackpush(p:Position):void := Ccode(void, "M2_stack.emplace_back(", p, ")" );
stackpop(e:Expr):Expr    := ( Ccode(void, "M2_stack.pop_back()"); e );

-- if traceDepth = 0, _prints_ the engine stacktrace using Boost::stacktrace
-- if traceDepth > 0, returns a sequence of positions from the parsing tree
-- TODO: limit this to Code, packages, stc for traceDepth = 1, 2, 3, etc.
export stacktrace(traceDepth:int):Expr := (
    if traceDepth == 0
    then ( Ccode(void, "profiler_stacktrace(std::cout, 0)"); nullE )
    else Expr(new Sequence len Ccode(int, "M2_stack.size()") at i do
	provide locate(Ccode(Position, "M2_stack[", i, "]"))));
stacktrace(e:Expr):Expr := (
    when e is n:ZZcell do if !isInt(n) then WrongArgSmallInteger(1)
    else stacktrace(toInt(n))
    else WrongArgSmallInteger(1));
setupfun("stacktrace", stacktrace);

-- this hash table will contain the accumulation of
-- profiling data (ticks and times) in each session
-- TODO: this should be thread local as well
ProfileTable := newHashTable(Tally, nothingClass);
ProfileTable.beingInitialized = false; -- mutex locks will occur
setupconst("ProfileTable", Expr(ProfileTable));

-- evaluates the code while measuring and storing the timing data
measure(c:Code):Expr := (
    key := locate(codePosition(c));
    data := wallTimer(c); -- calls evalraw, not eval
    -- TODO: optimize the bucket search
    current := lookup(ProfileTable, key);
    storeInHashTable(ProfileTable, key,
	Expr(when current is s:Sequence
	    do   Sequence(data.0 + s.0, oneE + s.1)
	    else Sequence(data.0,       oneE)));
    data.1);

-- when profiling = true is set, eval is substituted with evalprof,
-- so that everytime evalraw recursively calls eval, this one runs.
export evalprof(c:Code):Expr := (
    -- stdIO << tostring(codePosition(c)) << " " << tostring(c) << endl;
    stackpush(codePosition(c));
    stackpop(measure(c)));
evalprofpointer = evalprof;

-- briefly enables the profiler and evaluates the code that follows,
-- storing the resulting statistics in the global ProfileTable.
profile(c:Code):Expr := (
    -- stdError << " -- starting profiler" << endl;
    profiling = true;
    data := cpuTimer(c); -- calls eval, not evalraw
    profiling = false;
    -- stdError << " -- stopping profiler" << endl;
    -- now update the profiling totals
    key := toExpr("total");
    current := lookup(ProfileTable, key);
    storeInHashTable(ProfileTable, key,
	Expr(when current is s:Sequence
	    do   Sequence(data.0 + s.0, oneE + s.1)
	    else Sequence(data.0,       oneE)));
    data.1);
setupop(profileS, profile);
